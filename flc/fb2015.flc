
/*
	Example: Calculates informativity of an text sentence.

						By Jovani Brasil
*/

FUNCTION_BLOCK fb2015	// Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT				
	loc_len : REAL;
	k1 : REAL;
	k2 : REAL;
END_VAR

// Define output variable
VAR_OUTPUT				
	informatividade : REAL;
END_VAR

// Fuzzify input variable 'loc_len': {'baixo', 'medio' , 'alto'}
FUZZIFY loc_len			
	TERM baixo := gbell 0.241 2.040 0.046; 
	TERM medio := gbell 0.173 2.810 0.462;
	TERM alto := gbell 0.387 3.118 1.030;
END_FUZZIFY

// Fuzzify input variable 'k1': { 'baixo', 'medio', 'alto' }
FUZZIFY k1			
	TERM baixo := gbell 0.244 2.410 0.008;
	TERM medio := gbell 0.159 2.675 0.411;
	TERM alto := gbell 0.318 3.465 0.889;
END_FUZZIFY

// Fuzzify input variable 'k2': { 'baixo', 'medio', 'alto' }
FUZZIFY k2			
	TERM baixo := gbell 0.258 2.409 0.008;
	TERM medio := gbell 0.186 1.983 0.456;
	TERM alto := gbell 0.318 2.814 0.952;
END_FUZZIFY

// Defzzzify output variable 'informatividade' : {'baixo', 'medio', 'alto' }
DEFUZZIFY informatividade			
	TERM baixo := gbell 0.253 4.160 0.029;
	TERM medio := gbell 0.195 3.117 0.470;
	TERM alto := gbell 0.305 4.560 0.975;
	// DefuzzifierLeftMostMax (LM) DefuzzifierMeanMax (MM) DefuzzifierRightMostMax (RM)
	// DefuzzifierCenterOfArea (COA)
	METHOD : COG;		// Use 'Center Of Gravity' (COG) defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
	RANGE := (0 .. 1);
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1 : IF loc_len IS baixo AND k1 IS baixo AND k2 IS baixo THEN informatividade IS baixo;
	RULE 2 : IF loc_len IS baixo AND k1 IS baixo AND k2 IS medio THEN informatividade IS baixo;
	RULE 3 : IF loc_len IS baixo AND k1 IS baixo AND k2 IS alto THEN informatividade IS baixo;
	RULE 4 : IF loc_len IS baixo AND k1 IS medio AND k2 IS baixo THEN informatividade IS baixo;
	RULE 5 : IF loc_len IS baixo AND k1 IS medio AND k2 IS medio THEN informatividade IS medio;
	RULE 6 : IF loc_len IS baixo AND k1 IS medio AND k2 IS alto THEN informatividade IS medio;
	RULE 7 : IF loc_len IS baixo AND k1 IS alto AND k2 IS baixo THEN informatividade IS medio;
	RULE 8 : IF loc_len IS baixo AND k1 IS alto AND k2 IS medio THEN informatividade IS medio;
	RULE 9 : IF loc_len IS baixo AND k1 IS alto AND k2 IS alto THEN informatividade IS medio;
	RULE 10 : IF loc_len IS medio AND k1 IS baixo AND k2 IS baixo THEN informatividade IS baixo;
	RULE 11 : IF loc_len IS medio AND k1 IS baixo AND k2 IS medio THEN informatividade IS medio;
	RULE 12 : IF loc_len IS medio AND k1 IS baixo AND k2 IS alto THEN informatividade IS medio;
	RULE 13 : IF loc_len IS medio AND k1 IS medio AND k2 IS baixo THEN informatividade IS baixo;
	RULE 14 : IF loc_len IS medio AND k1 IS medio AND k2 IS medio THEN informatividade IS medio;
	RULE 15 : IF loc_len IS medio AND k1 IS medio AND k2 IS alto THEN informatividade IS alto;
	RULE 16 : IF loc_len IS medio AND k1 IS alto AND k2 IS baixo THEN informatividade IS medio;
	RULE 17 : IF loc_len IS medio AND k1 IS alto AND k2 IS medio THEN informatividade IS alto;
	RULE 18 : IF loc_len IS medio AND k1 IS alto AND k2 IS alto THEN informatividade IS alto;
	RULE 19 : IF loc_len IS alto AND k1 IS baixo AND k2 IS baixo THEN informatividade IS medio;
	RULE 20 : IF loc_len IS alto AND k1 IS baixo AND k2 IS medio THEN informatividade IS medio;
	RULE 21 : IF loc_len IS alto AND k1 IS baixo AND k2 IS alto THEN informatividade IS alto;
	RULE 22 : IF loc_len IS alto AND k1 IS medio AND k2 IS baixo THEN informatividade IS medio;
	RULE 23 : IF loc_len IS alto AND k1 IS medio AND k2 IS medio THEN informatividade IS alto;
	RULE 24 : IF loc_len IS alto AND k1 IS medio AND k2 IS alto THEN informatividade IS alto;
	RULE 25 : IF loc_len IS alto AND k1 IS alto AND k2 IS baixo THEN informatividade IS alto;
	RULE 26 : IF loc_len IS alto AND k1 IS alto AND k2 IS medio THEN informatividade IS alto;
	RULE 27 : IF loc_len IS alto AND k1 IS alto AND k2 IS alto THEN informatividade IS alto;	
	
END_RULEBLOCK

END_FUNCTION_BLOCK
